plugins {
	id 'java'
	id 'application'
	id 'org.javamodularity.moduleplugin' version '1.8.12'
	id 'org.openjfx.javafxplugin' version '0.0.13'
	id 'org.beryx.jlink' version '2.25.0'
}

group 'edu.umich.soar.svsviewer'
version '1.0.0'

repositories {
	mavenCentral()
}

// No need to target a lower version, since we distribute a standalone application
java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

ext {
	junitVersion = '5.9.2'
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
}

application {
	mainModule = 'edu.umich.soar.svsviewer'
	mainClass = 'edu.umich.soar.svsviewer.SvsViewerApplication'
//	mainClass = 'edu.umich.soar.svsviewer.CubeViewer'
}

javafx {
	version = '21.0.4'
	modules = ['javafx.controls', 'javafx.fxml', 'javafx.swing']
}

dependencies {
	// https://mvnrepository.com/artifact/jakarta.annotation/jakarta.annotation-api
	implementation group: 'jakarta.annotation', name: 'jakarta.annotation-api', version: '3.0.0'

	implementation group: 'com.github.quickhull3d', name: 'quickhull3d', version: '1.0.0'

	implementation 'org.fxyz3d:fxyz3d:0.6.0'

	testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
	testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")

	testImplementation group: "org.assertj", name: "assertj-core", version: "3.26.3"
}

test {
	useJUnitPlatform()
}

jlink {
	imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
	options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
	launcher {
		name = 'SVSViewer'
	}
	// addresses https://github.com/beryx/badass-jlink-plugin/issues/163
	addExtraDependencies("javafx")
	mergedModule {
		// required by quickhull3d, which isn't a module, so we have to add it explicitly here
		requires 'org.slf4j'
	}
}

jlinkZip {
	group = 'distribution'
}

jpackage {
	setApplicationName("SVSViewer")
}
